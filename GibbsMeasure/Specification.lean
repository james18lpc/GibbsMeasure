import Mathlib.Order.Ideal
import GibbsMeasure.Mathlib.Data.Finset.Basic
import GibbsMeasure.Mathlib.MeasureTheory.Measure.GiryMonad
import GibbsMeasure.KolmogorovExtension4.ProductMeasure
import GibbsMeasure.Prereqs.Juxt
import GibbsMeasure.Prereqs.Filtration.Consistent
import GibbsMeasure.Prereqs.Kernel.Proper

/-!
# Gibbs measures

This file defines Gibbs measures.
-/

open ProbabilityTheory Set MeasureTheory ENNReal NNReal

variable {S E : Type*} {mE : MeasurableSpace E} {Î›â‚ Î›â‚‚ : Finset S}

/-- A family of kernels `Î³` is consistent if `Î³ Î›â‚ âˆ˜â‚– Î³ Î›â‚‚ = Î³ Î›â‚‚` for all `Î›â‚ âŠ† Î›â‚‚`.

Morally, the LHS should be thought of as discovering `Î›â‚` then `Î›â‚‚`, while the RHS should be
thought of as discovering `Î›â‚‚` straight away. -/
def IsConsistent (Î³ : âˆ€ Î› : Finset S, Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E)) : Prop :=
  âˆ€ â¦ƒÎ›â‚ Î›â‚‚â¦„, Î›â‚ âŠ† Î›â‚‚ â†’ (Î³ Î›â‚).comap id cylinderEvents_le_pi âˆ˜â‚– Î³ Î›â‚‚ = Î³ Î›â‚‚

lemma isConsistentKernel_cylinderEventsCompl
    {Î³ : âˆ€ Î› : Finset S, Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E)} :
    Filtration.cylinderEventsCompl.IsConsistentKernel (fun Î› â†¦ Î³ (OrderDual.ofDual Î›)) â†”
      IsConsistent Î³ := forall_swap

variable (S E) in
/-- A specification from `S` to `E` is a collection of "boundary condition kernels" on the
complement of finite sets, compatible under restriction.

The term "boundary condition kernels" is chosen because for a Gibbs measure associated to
a specification, the kernels of the specification are precisely the regular conditional
probabilities of the Gibbs measure conditionally on the configurations in the complements of
finite sets (which serve as "boundary conditions"). -/
structure Specification [MeasurableSpace E] where
  /-- The boundary condition kernels of a specification.

  DO NOT USE. Instead use the coercion to function `â‡‘Î³`. Lean should insert it automatically in most
  cases. -/
  toFun (Î› : Finset S) : Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E)
  /-- The boundary condition kernels of a specification are consistent.

  DO NOT USE. Instead use `Specification.isConsistent`. -/
  isConsistent' : IsConsistent toFun

namespace Specification

instance instDFunLike :
    DFunLike (Specification S E) (Finset S) fun Î› â†¦ Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E)
    where
  coe := toFun
  coe_injective' Î³â‚ Î³â‚‚ h := by cases Î³â‚; cases Î³â‚‚; congr

/-- The boundary condition kernels of a specification are consistent. -/
lemma isConsistent (Î³ : Specification S E) : IsConsistent Î³ := Î³.isConsistent'

initialize_simps_projections Specification (toFun â†’ apply)

variable {Î³ Î³â‚ Î³â‚‚ : Specification S E}

@[ext] lemma ext : (âˆ€ Î›, Î³â‚ Î› = Î³â‚‚ Î›) â†’ Î³â‚ = Î³â‚‚ := DFunLike.ext _ _

section IsMarkov

/-- A Markov specification is a specification whose boundary condition kernels are all Markov
kernels. -/
def IsMarkov (Î³ : Specification S E) : Prop := âˆ€ Î›, IsMarkovKernel (Î³ Î›)

end IsMarkov

section IsProper

/-- A specification is proper if all its boundary condition kernels are. -/
def IsProper (Î³ : Specification S E) : Prop := âˆ€ Î› : Finset S, (Î³ Î›).IsProper

lemma isProper_iff_restrict_eq_indicator_smul :
    Î³.IsProper â†”
      âˆ€ (Î› : Finset S) â¦ƒB : Set (S â†’ E)â¦„ (hB : MeasurableSet[cylinderEvents Î›á¶œ] B) (x : S â†’ E),
      (Î³ Î›).restrict (cylinderEvents_le_pi _ hB) x = B.indicator (1 : (S â†’ E) â†’ â„â‰¥0âˆ) x â€¢ Î³ Î› x :=
  forall_congr' fun _ â†¦ Kernel.isProper_iff_restrict_eq_indicator_smul _

lemma isProper_iff_inter_eq_indicator_mul :
    Î³.IsProper â†”
      âˆ€ (Î› : Finset S) â¦ƒA : Set (S â†’ E)â¦„ (_hA : MeasurableSet A) â¦ƒB : Set (S â†’ E)â¦„
        (_hB : MeasurableSet[cylinderEvents Î›á¶œ] B) (Î· : S â†’ E),
      Î³ Î› Î· (A âˆ© B) = B.indicator 1 Î· * Î³ Î› Î· A :=
  forall_congr' fun _ â†¦ Kernel.isProper_iff_inter_eq_indicator_mul cylinderEvents_le_pi

alias âŸ¨IsProper.restrict_eq_indicator_smul, IsProper.of_restrict_eq_indicator_smulâŸ© :=
  isProper_iff_restrict_eq_indicator_smul

alias âŸ¨IsProper.inter_eq_indicator_mul, IsProper.of_inter_eq_indicator_mulâŸ© :=
  isProper_iff_inter_eq_indicator_mul

variable {A B : Set (S â†’ E)} {f g : (S â†’ E) â†’ â„â‰¥0âˆ} {Î·â‚€ : S â†’ E}

lemma IsProper.setLintegral_eq_indicator_mul_lintegral (hÎ³ : Î³.IsProper) (Î› : Finset S)
    (hf : Measurable f) (hB : MeasurableSet[cylinderEvents Î›á¶œ] B) :
    âˆ«â» x in B, f x âˆ‚(Î³ Î› Î·â‚€) = B.indicator 1 Î·â‚€ * âˆ«â» x, f x âˆ‚(Î³ Î› Î·â‚€) :=
  (hÎ³ Î›).setLintegral_eq_indicator_mul_lintegral cylinderEvents_le_pi hf hB _

lemma IsProper.setLintegral_inter_eq_indicator_mul_setLintegral (Î› : Finset S) (hÎ³ : Î³.IsProper)
    (hf : Measurable f) (hA : MeasurableSet A) (hB : MeasurableSet[cylinderEvents Î›á¶œ] B) :
    âˆ«â» x in A âˆ© B, f x âˆ‚(Î³ Î› Î·â‚€) = B.indicator 1 Î·â‚€ * âˆ«â» x in A, f x âˆ‚(Î³ Î› Î·â‚€) :=
  (hÎ³ Î›).setLintegral_inter_eq_indicator_mul_setLintegral cylinderEvents_le_pi hf hA hB _

lemma IsProper.lintegral_mul (hÎ³ : Î³.IsProper) (Î› : Finset S) (hf : Measurable f)
    (hg : Measurable[cylinderEvents Î›á¶œ] g) :
    âˆ«â» x, f x * g x âˆ‚(Î³ Î› Î·â‚€) = g Î·â‚€ * âˆ«â» x, f x âˆ‚(Î³ Î› Î·â‚€) :=
  (hÎ³ _).lintegral_mul cylinderEvents_le_pi hf hg _

end IsProper

section IsGibbsMeasure
variable {Î¼ : Measure (S â†’ E)}

/-- For a specification `Î³`, a Gibbs measure is a measure whose conditional expectation kernels
conditionally on configurations exterior to finite sets agree with the boundary condition kernels
of the specification `Î³`. -/
def IsGibbsMeasure (Î³ : Specification S E) (Î¼ : Measure (S â†’ E)) : Prop :=
  âˆ€ (Î› : Finset S) (A : Set (S â†’ E)), MeasurableSet A â†’
    condexp (cylinderEvents Î›á¶œ) Î¼ (A.indicator fun _ â†¦ 1) =áµ[Î¼] fun Ïƒ â†¦ (Î³ Î› Ïƒ A).toReal

lemma isGibbsMeasure_tfae (Î³ : Specification S E) (Î¼ : Measure (S â†’ E)) :
    List.TFAE [
      Î³.IsGibbsMeasure Î¼,
      âˆ€ Î›, Î¼.bind (Î³ Î›) = Î¼,
      âˆƒ ğ“¢ : Order.Cofinal (Finset S), (âˆ€ Î› âˆˆ ğ“¢.carrier, Î¼.bind (Î³ Î›) = Î¼)] := by
  tfae_have 1 â†” 2
  Â· sorry
  tfae_have 2 â†’ 3
  Â· sorry
  sorry

end IsGibbsMeasure

noncomputable section ISSSD
variable (Î½ : Measure E) (Î· : S â†’ E)

-- TODO: Use `measurable_of_measurable_coe'` + measurable rectangles here
private lemma measurable_isssdFun (Î› : Finset S) :
    Measurable[cylinderEvents Î›á¶œ]
      fun Î· : S â†’ E â†¦ (Measure.pi fun _ : Î› â†¦ Î½).map (juxt E Î› Î·) := by
  refine @Measure.measurable_of_measurable_coe _ _ _ (_) _ ?_
  rintro A hA
  simp
  sorry

/-- Auxiliary definition for `Specification.isssd`. -/
@[simps (config := .asFn)]
def isssdFun (Î½ : Measure E) (Î› : Finset S) :
    Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E) :=
  @Kernel.mk _ _ (_) _
    (fun Î· â†¦ Measure.map (juxt E Î› Î·) (Measure.pi fun _ : Î› â†¦ Î½))
    (measurable_isssdFun Î½ Î›)

/-- The ISSSD of a measure is strongly consistent. -/
lemma isssdFun_comp_isssdFun [DecidableEq S] (Î›â‚ Î›â‚‚ : Finset S) :
    (isssdFun Î½ Î›â‚).comap id cylinderEvents_le_pi âˆ˜â‚– isssdFun Î½ Î›â‚‚ =
      (isssdFun Î½ (Î›â‚ âˆª Î›â‚‚)).comap id
        (measurable_id'' $ by gcongr; exact Finset.subset_union_right) :=
  sorry

/-- The **Independent Specification with Single Spin Distribution**.

This is the specification corresponding to the product measure -/
@[simps]
def isssd : Specification S E where
  toFun := isssdFun Î½
  isConsistent' Î›â‚ Î›â‚‚ hÎ› := by
    classical
    rw [isssdFun_comp_isssdFun]
    ext a s _
    simp only [Kernel.comap_apply, id_eq, isssdFun_apply, Finset.coe_sort_coe]
    rw [Finset.union_eq_right.2 hÎ›]

/-- The ISSSD of a measure is strongly consistent. -/
lemma isssd_comp_isssd [DecidableEq S] (Î›â‚ Î›â‚‚ : Finset S) :
    (isssd Î½ Î›â‚).comap id cylinderEvents_le_pi âˆ˜â‚– isssd Î½ Î›â‚‚ =
      (isssd Î½ (Î›â‚ âˆª Î›â‚‚)).comap id
        (measurable_id'' $ by gcongr; exact Finset.subset_union_right) := isssdFun_comp_isssdFun ..

protected lemma IsProper.isssd : (isssd (S := S) Î½).IsProper := by
  refine IsProper.of_inter_eq_indicator_mul fun Î› A hA B hB x â†¦ ?_
  simp only [isssd_apply, isssdFun_apply, Finset.coe_sort_coe]
  sorry

end ISSSD

section ProductMeasure

/-- The product measure `Î½ ^ S` is a `isssd Î¼`-Gibbs measure. -/
lemma isGibbsMeasure_isssd_productMeasure (Î½ : Measure E) [IsProbabilityMeasure Î½] :
    (isssd Î½).IsGibbsMeasure (productMeasure fun _ : S â†¦  Î½) := by
  rintro Î›
  sorry

end ProductMeasure

section Modification
variable {Ï : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ}

/-- The kernel of a modified specification.

Modifying the specification `Î³` by a family indexed by finsets `Î› : Finset S` of densities
`Ï Î› : (S â†’ E) â†’ â„â‰¥0âˆ` results in a family of kernels `Î³.modifiedKer Ï _ Î›` whose density is that of
`Î³ Î›` multiplied by `Ï Î›`.

This is an auxiliary definition for `Specification.modified`, which you should generally use instead
of `Specification.modifiedKer`. -/
@[simps]
noncomputable def modifiedKer (Î³ : âˆ€ Î› : Finset S, Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E))
    (Ï : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ) (hÏ : âˆ€ Î›, Measurable (Ï Î›)) (Î› : Finset S) :
    Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E) :=
  @Kernel.mk _ _ (_) _
    (fun Î· â†¦ (Î³ Î› Î·).withDensity (Ï Î›))
    (@Measure.measurable_of_measurable_coe _ _ _ (_) _ fun s hs â†¦ by
      simp_rw [MeasureTheory.withDensity_apply _ hs]
      exact (Measure.measurable_setLintegral (hÏ _) hs).comp (Î³ Î›).measurable)

@[simp] lemma modifiedKer_one' (Î³ : âˆ€ Î› : Finset S, Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E)) :
    modifiedKer Î³ (fun _Î› _Î· â†¦ 1) (fun _Î› â†¦ measurable_const) = Î³ := by ext Î›; simp

@[simp] lemma modifiedKer_one (Î³ : âˆ€ Î› : Finset S, Kernel[cylinderEvents Î›á¶œ] (S â†’ E) (S â†’ E)) :
    modifiedKer Î³ 1 (fun _Î› â†¦ measurable_const) = Î³ := by ext Î›; simp

/-- A modification of a specification `Î³` is a family indexed by finsets `Î› : Finset S` of densities
`Ï Î› : (S â†’ E) â†’ â„â‰¥0âˆ` such that:
* Each `Ï Î›` is measurable.
* `Î³.modifiedKer Ï` (informally, `Ï * Î³`) is consistent. -/
structure IsModification (Î³ : Specification S E) (Ï : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ) : Prop where
  measurable Î› : Measurable (Ï Î›)
  isConsistent : IsConsistent (modifiedKer Î³ Ï measurable)

@[simp] lemma IsModification.one' : Î³.IsModification (fun _Î› _Î· â†¦ 1) where
  measurable _ := measurable_const
  isConsistent := by simpa using Î³.isConsistent

@[simp] lemma IsModification.one : Î³.IsModification 1 := .one'

/-- Modified specification.

Modifying the specification `Î³` by a family indexed by finsets `Î› : Finset S` of densities
`Ï Î› : (S â†’ E) â†’ â„â‰¥0âˆ` results in a family of kernels `Î³.modifiedKer Ï _ Î›` whose density is that of
`Î³ Î›` multiplied by `Ï Î›`.

When the family of densities `Ï` is a modification (`Specification.IsModification`), modifying a
specification results in a specification `Î³.modified Ï _`. -/
noncomputable def modified (Î³ : Specification S E) (Ï : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ)
    (hÏ : Î³.IsModification Ï) : Specification S E where
  toFun := modifiedKer Î³ Ï hÏ.measurable
  isConsistent' := hÏ.isConsistent

-- This is not simp as we want to keep `modifiedKer` an implementation detail
lemma coe_modified (Î³ : Specification S E) (Ï : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ)
    (hÏ : Î³.IsModification Ï) : Î³.modified Ï hÏ = modifiedKer Î³ Ï hÏ.measurable := rfl

@[simp]
lemma modified_apply (Î³ : Specification S E) (Ï : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ)
    (hÏ : Î³.IsModification Ï) (Î› : Finset S) (Î· : S â†’ E) :
    Î³.modified Ï hÏ Î› Î· = (Î³ Î› Î·).withDensity (Ï Î›) := rfl

@[simp] lemma IsModification.mul {Ïâ‚ Ïâ‚‚ : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ}
    (hÏâ‚ : Î³.IsModification Ïâ‚) (hÏâ‚‚ : (Î³.modified Ïâ‚ hÏâ‚).IsModification Ïâ‚‚) :
    Î³.IsModification (Ïâ‚ * Ïâ‚‚) where
  measurable Î› := (hÏâ‚.measurable _).mul (hÏâ‚‚.measurable _)
  isConsistent := sorry

@[simp] lemma modified_one' (Î³ : Specification S E) : Î³.modified (fun _Î› _Î· â†¦ 1) .one' = Î³ := by
  ext; simp

@[simp] lemma modified_one (Î³ : Specification S E) : Î³.modified 1 .one = Î³ := by ext; simp

@[simp] lemma modified_modified (Î³ : Specification S E) (Ïâ‚ Ïâ‚‚ : Finset S â†’ (S â†’ E) â†’ â„â‰¥0âˆ)
    (hÏâ‚ : Î³.IsModification Ïâ‚) (hÏâ‚‚ : (Î³.modified Ïâ‚ hÏâ‚).IsModification Ïâ‚‚) :
    (Î³.modified Ïâ‚ hÏâ‚).modified Ïâ‚‚ hÏâ‚‚ = Î³.modified (Ïâ‚ * Ïâ‚‚) (hÏâ‚.mul hÏâ‚‚) := sorry

protected lemma IsProper.modified (hÎ³ : Î³.IsProper) {hÏ} : (Î³.modified Ï hÏ).IsProper := by
  refine IsProper.of_inter_eq_indicator_mul fun Î› A hA B hB Î· â†¦ ?_
  rw [modified_apply, withDensity_apply _ hA,
    withDensity_apply _ (hA.inter $ cylinderEvents_le_pi _ hB),
    hÎ³.setLintegral_inter_eq_indicator_mul_setLintegral _ (hÏ.measurable _) hA hB]

end Modification
end Specification
